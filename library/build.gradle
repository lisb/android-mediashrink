apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    namespace "com.lisb.android.mediashrink"
    compileSdk android_compile_sdk_version

    defaultConfig {
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    api fileTree(dir: 'libs', include: '*.jar')
    api 'com.jakewharton.timber:timber:5.0.1'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.source
}

afterEvaluate {
    def localProps = new Properties()
    localProps.load(rootProject.file('local.properties').newDataInputStream())

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/android-mediashrink"
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                groupId = 'com.lisb.android.mediashrink'
                artifactId = 'mediashrink'
                version = '2.0.3-SNAPSHOT'
            }
        }
    }
}
